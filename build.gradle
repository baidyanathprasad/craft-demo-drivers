group 'com.baidyanath'
version project.'com.baidyanath.version'

buildscript {
    // Core dependencies
    ext.kotlinVersion = '1.4.30'
    ext.dropwizardVersion = '1.3.17'
    ext.springDependencyManagementVersion = '1.0.3.RELEASE'
    ext.mariaDbConnectorVersion = '2.2.4'
    ext.swaggerVersion = '2.0.0-1'

    // Test dependencies
    ext.junitPlatformVersion = '1.0.0'
    ext.spekVersion = '1.1.5'
    ext.shadowVersion = '5.2.0'
    ext.junit = '4.11'

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadowVersion"
        classpath "io.spring.gradle:dependency-management-plugin:$springDependencyManagementVersion"
    }
}

repositories {
    jcenter()
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'org.junit.platform.gradle.plugin'

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs = [
                '-Xjvm-default=enable' //Needed for using @JvmDefault
        ]
    }
}

// Use $./gradlew run -PappArgs="['server', 'appconfig-dev.yml']" to run from gradle directly
mainClassName = 'com.baidyanath.AppKt'

run {
    if (project.hasProperty('appArgs')) {
        args Eval.me(appArgs)
    }
}

shadowJar {
    baseName = 'craftdemo'
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

junitPlatform {
    filters {
        engines {
            include 'spek'
        }
    }
}

test {
    testLogging {
        events 'PASSED', 'SKIPPED', 'FAILED', 'STANDARD_OUT', 'STANDARD_ERROR'
        showStackTraces true
        exceptionFormat 'FULL'
        showStandardStreams true
    }
}

sourceCompatibility = 1.8

dependencies {
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlinVersion
    implementation group: 'io.dropwizard', name: 'dropwizard-core', version: dropwizardVersion
    implementation group: 'io.dropwizard', name: 'dropwizard-jdbi3', version: dropwizardVersion
    implementation group: 'io.dropwizard', name: 'dropwizard-assets', version: dropwizardVersion
    implementation group: 'io.dropwizard', name: 'dropwizard-views-mustache', version: dropwizardVersion
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client',
            version: mariaDbConnectorVersion
    implementation group: 'io.dropwizard', name: 'dropwizard-migrations', version: dropwizardVersion
    implementation group: 'com.smoketurner', name: 'dropwizard-swagger', version: swaggerVersion

    testImplementation group: 'io.dropwizard', name: 'dropwizard-testing', version: dropwizardVersion
    testImplementation group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlinVersion


    testImplementation(group: 'org.jetbrains.spek', name: 'spek-api', version: spekVersion) {
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation(group: 'org.jetbrains.spek', name: 'spek-subject-extension',
            version: spekVersion) {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.jetbrains.spek'
    }
    testRuntime(group: 'org.jetbrains.spek', name: 'spek-junit-platform-engine',
            version: spekVersion) {
        exclude group: 'org.junit.platform'
        exclude group: 'org.jetbrains.kotlin'
    }

    testRuntime "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
    testImplementation group: 'junit', name: 'junit', version: junit
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

wrapper {
    gradleVersion = '6.0.1'
    distributionUrl = $/https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip/$
}